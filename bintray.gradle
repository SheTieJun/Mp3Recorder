apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'  //这个也是必须加的否则会socket 超时？？？

def libName = bintrayConfig.libName
def groupName = bintrayConfig.groupName
def versionName = bintrayConfig.versionName

def siteUrl = bintrayConfig.siteUrl
def gitUrl = bintrayConfig.gitUrl
def issueUrl = bintrayConfig.issueUrl

def developerId = bintrayConfig.developerId
def developerName = bintrayConfig.developerName
def developerEmail = bintrayConfig.developerEmail


def pkgIng = bintrayConfig.pkgIng
def pkgDes = bintrayConfig.pkgDes
def pkgUserOrg = bintrayConfig.pkgUserOrg
def pkgRepo = bintrayConfig.pkgRepo

//使用： 'groupName:versionName:versionName'
group = groupName
version = versionName
project.archivesBaseName = libName


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging pkgIng
                name libName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }

}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//userOrg – an optional organization name when the repo belongs to one of the user's orgs. If not added will use 'BINTRAY_USER' by default
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = pkgUserOrg
        repo = pkgRepo
        name = libName
        desc = pkgDes
        licenses = ["Apache-2.0"]
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        publish = true
        publicDownloadNumbers = true
    }
}
